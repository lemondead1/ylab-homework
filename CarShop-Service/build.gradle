plugins {
    id 'application'
    id 'io.freefair.lombok' version '8.6'
    id 'org.springframework.boot' version "${spring_boot_version}"
}

group = 'com.lemondead1'
version = '0.0.1'

application {
    mainClass = "com.lemondead1.carshopservice.CarShopServiceApplication"
}

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstruct_version}"
    implementation "org.mapstruct:mapstruct:${mapstruct_version}"

    implementation("org.springframework.boot:spring-boot-starter-web") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-tomcat"
    }
    implementation "org.springframework.boot:spring-boot-starter-jetty"
    implementation "org.springframework.boot:spring-boot-starter-aop"
    implementation "org.liquibase:liquibase-core"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    implementation "org.eclipse.jetty.ee10:jetty-ee10-webapp:${jetty_version}"
    implementation "ch.qos.logback:logback-classic:${logback_version}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdoc_version}"

    implementation project(":CarShop-Logging")
    implementation project(":CarShop-Audit")

    runtimeOnly "org.postgresql:postgresql"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.testcontainers:testcontainers"
    testImplementation "org.testcontainers:postgresql"
}

compileJava {
    options.compilerArgs << '-parameters'
}

test {
    useJUnitPlatform()
}

tasks.register('buildDockerImage', Exec) {
    group = 'build'
    dependsOn bootJar

    workingDir = "${projectDir}"
    commandLine "docker", "build", "-t=carshop-service", "--build-arg=JAR_PATH=build/libs/CarShop-Service.jar", "."
}

// It does not work anyway.
bootBuildImage.enabled = false

tasks.register('runDocker', Exec) {
    dependsOn buildDockerImage

    commandLine "docker", "compose", "pull"
    commandLine "docker", "compose", "up", "-d"
}

build {
    dependsOn buildDockerImage
}